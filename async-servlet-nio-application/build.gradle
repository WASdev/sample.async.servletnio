apply plugin: 'war'
apply plugin: 'eclipse-wtp'
apply plugin: 'maven-publish'

// Use the Liberty Gradle plugin to run our functional tests
apply plugin: 'liberty'

buildscript {
    repositories {
        mavenCentral()
        maven {
            name = 'Sonatype Nexus Snapshots'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
    dependencies {
        classpath 'net.wasdev.wlp.gradle.plugins:liberty-gradle-plugin:1.0-SNAPSHOT'
    }
}


// Dependencies
repositories { mavenCentral(); }

dependencies {
    providedCompile group:'javax.servlet', name:'javax.servlet-api', version:'3.1.0'
    testCompile group:'javax.ws.rs', name:'javax.ws.rs-api', version:'2.0'
    testCompile 'junit:junit:4.12'
    testRuntime 'org.apache.cxf:cxf-rt-rs-client:3.1.1'
    testRuntime 'org.glassfish:javax.json:1.0.4'
}

sourceCompatibility = 1.7

// Set the Eclipse facets to use 3.1 of the Dynamic Web Module which requires Java 1.7 by default
eclipse.wtp.facet {
    // Clear default facets to work around bug where you get duplicates including wst 2.4
    facets = []
    facet name: 'jst.java', type: 'fixed'
    facet name: 'jst.web', type: 'fixed'
    facet name: 'jst.web', version: '3.1'
    facet name: 'jst.java', version: '1.7'
}

// Use a templated loose config XML and copy this into the usr dir as part of the assemble stage
task publishWar(dependsOn: 'jar', type: Copy) {
    from(file(new File(sourceSets.main.output.resourcesDir, "gradle.async-servlet-nio-application.war.xml")))
    def properties = [webAppDir : webAppDir, classesDir : sourceSets.main.output.classesDir]
    expand(properties)
    rename { String fileName ->
        fileName.replace('gradle.', '')
    }
    into('../async-servlet-nio-wlpcfg/servers/servletNioSample/apps')
}

assemble.dependsOn('publishWar')

// Publish WAR to local maven
publishing {
    publications {
        mavenWar(MavenPublication) {
            version '1.0-SNAPSHOT'
            groupId 'net.wasdev.wlp.sample'
            artifactId 'async-servlet-nio-application'
            
            from components.web
        }
    }
}

// Add a functional test task that runs during the 'check' phase that uses the Liberty Gradle plugin
// to launch Liberty and run the test classes in the fvt source set
task fvt(type: Test, dependsOn: jar) {
    group 'Verification'
    description 'Runs the functional verification tests.'
    reports.html.destination = file("$buildDir/reports/fvt")
    reports.junitXml.destination = file("$buildDir/test-results/fvt")
    include '**/fvt/**'
    exclude '**/unit/**'
    systemProperties = ['liberty.test.port': getHttpPort()]
    dependsOn publishWar, libertyStart
    finalizedBy libertyStop
}

test {
    reports.html.destination = file("$buildDir/reports/unit")
    reports.junitXml.destination = file("$buildDir/test-results/unit")
    include '**/unit/**'
    exclude '**/fvt/**'
}

String getHttpPort() {
    def DEFAULT_HTTP_PORT = 9083
    def serverXmlFile = file(new File('../async-servlet-nio-wlpcfg/servers/servletNioSample/server.xml'))
    def portPattern = ~/.*httpPort="(\d+)".*/
    serverXmlFile.eachLine { line -> 
        def portMatcher = line =~ portPattern
        if (portMatcher) {
            return portMatcher.group(1)
        }
    }
    return DEFAULT_HTTP_PORT
}

libertyStart.mustRunAfter publishWar

liberty {
    wlpDir = owner.hasProperty('libertyRoot') ? "${libertyRoot}" : null
    serverName = 'servletNioSample'
    userDir = '../async-servlet-nio-wlpcfg'
    clean = true
}

if (hasProperty('libertyRoot') && file("${libertyRoot}").exists() && file("${libertyRoot}/lib/ws-launch.jar").exists()) {
    check.dependsOn fvt
} else {
    logger.error('Unable to run integration tests because the libertyRoot property is not set to a directory that contains the /lib/ws-launch.jar file.  Set it in async-servlet-nio-application/gradle.properties to your Liberty installation.')
}
